apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lcq.interviewquestion"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
        wandoujia {}
        xiaomi {}
        _360 {}
        //...
    }
    flavorDimensions "channel"
    productFlavors.all {
            //批量修改，类似一个循序遍历
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    signingConfigs {// 自动化打包配置
        release {// 线上环境
            keyAlias 'test1'
            keyPassword '123456'
            storeFile file('test.jks')
            storePassword '123456'
        }
        debug {// 开发环境
            keyAlias 'test1'
            keyPassword '123456'
            storeFile file('test.jks')
            storePassword '123456'
        }
    }

    buildTypes { //生产环境和测试环境配置
        release {

            minifyEnabled true//是否对代码进行混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定混淆的规则文件
            signingConfig signingConfigs.release
            //输出文件名
            // 更新至Android Studio 3.3 gradle 4.10.1
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.productFlavors[0].name}-${variant.versionName}-${releaseTime()}.apk"
                }
            }
//            applicationVariants.all { variant ->
//                variant.outputs.all { output ->
//                    def outputFile = output.outputFile
//                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                        // 输出apk名称为:渠道_v版本号.apk，如（100001_v1.0.0.apk）
//                        def fileName = "${variant.productFlavors[0].name}_v${defaultConfig.versionName}.apk"
//                        outputFileName = new File(fileName)
//                    }
//                }
//            }
        }
        debug {
            minifyEnabled false//是否对代码进行混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定混淆的规则文件
            signingConfig signingConfigs.debug//设置签名信息
            debuggable false//是否支持断点调试
            jniDebuggable false//是否可以调试NDK代码
        }
    }


}
//获取编译时间
static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.13-beta-2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-21'

    debugImplementation 'com.apkfuns.logutils:library:1.7.5'
    releaseImplementation 'com.apkfuns.logutils:logutils-no-op:1.7.5'
}
